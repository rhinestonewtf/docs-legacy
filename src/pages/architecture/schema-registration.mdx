import { Tag } from '@/components/Tag'
import { Link } from '@/components/Link'

# Schema Registration

> Schemas allow for flexible attestations, giving smart account developers and auditors to create attestations in the way that best fits their use case.

The Schema Registration function is responsible for registering new schemas in the Rhinestone Registry. A schema is composed of an ABI (Application Binary Interface) description of data saved in attestations, as well as the resolver address. By allowing for flexible schemas, the Rhinestone Registry allows for extreme flexibility in the structure of the data stored in attestations, giving smart account developers and auditors to create attestations in the way that best fits their use case.

<Fence className="bg-gray-100">
![Schema Flow](/images/docs/introduction/schema-flow.svg)
</Fence>

### Parameters

<Note title="caution" type="info">
  Schemas can be defined in typical ABI format.
</Note>

When calling the `registerSchema` function on the Rhinestone Registry, the following parameters are required:

```solidity
function registerSchema(
    string calldata schema,
    ISchemaResolver resolver,
    bool revocable
)
    external
    returns (bytes32);
```

After a successful schema registry, the Rhinestone Registry will return a unique identifier for the schema. This identifier can be used to reference the schema in future operations. 


```solidity
struct SchemaRecord {
    bytes32 uid; // The unique identifier of the schema.
    ISchemaResolver resolver; // Optional schema resolver.
    bool revocable; // Whether the schema allows revocations explicitly.
    string schema; // Custom specification of the schema (e.g., an ABI).
    address schemaOwner; // The address of the account used to register the schema.
}
```




## Schema Resolver

The SchemaResolver plays a vital role in the Rhinestone Registry's infrastructure, serving as a conduit for verifying and managing the attestations related to each registered schema. Essentially, the SchemaResolver is an interface definition that interacts with the Rhinestone Registry to register schemas and control how attestations are formed and validated.

Entities that register schemas on the registry elect to provide a SchemaResolver. During attestations, revocations, and module registrations, the registry calls hooks on the provided SchemaResolver, allowing it to act on the associated operations. This interaction empowers the SchemaResolver to govern the integrity and correctness of the attestations.

### Functionality

A SchemaResolver is associated with a particular schema at the time of schema registration. The SchemaResolver provides an interface for the Rhinestone Registry to interact with the schema, specifically to encode, decode, and verify the attestation data against the schema. This ensures that the data being attested is in line with the schema and can be correctly interpreted by anyone accessing the attestation.

### Responsibilities
The primary responsibilities of the SchemaResolver include:

- **Validation:** The SchemaResolver is responsible for ensuring that all attestations adhere to the associated schema. It performs validation on the data being attested, ensuring that it meets the requirements specified in the schema.
- **Encoding and Decoding:** To manage the storage and retrieval of attestations, the SchemaResolver must be able to correctly encode and decode attestation data according to the schema. This enables the data to be stored compactly on-chain, while still maintaining its meaningfulness off-chain.
- **Hook Calls:** During certain operations such as attestations, revocations, and module registrations, the Rhinestone Registry will call hooks on the SchemaResolver. These hooks allow the SchemaResolver to perform any necessary actions related to these operations. For example, an attestation hook might perform additional checks on the data or update some internal state.

### Extensibility
The SchemaResolver provides a extensibility to the Rhinestone Registry. By allowing entities to provide their own SchemaResolver when registering a schema, the Rhinestone Registry can support a wide variety of data formats and validation rules. This extensibility makes the Rhinestone Registry versatile enough to support the diverse needs of different smart account modules.


<Note title="caution" type="info">
The SchemaResolver is a critical component of the Rhinestone Registry, ensuring data integrity and consistency throughout the system. It guarantees that the data being attested meets the necessary validation criteria, while also maintaining a level of extensibility, thus enhancing the system's overall flexibility and robustness.
</Note>



### Example: TokenResolver

The TokenResolver is a resolver contract used for schema resolution that verifies if a designated quantity of tokens have received approval for attestation. Upon submission of an attestation, the resolver oversees the necessary token transfer from the address of the attester to its own address. This contract makes use of both the ERC20 standard and the SafeERC20 library to ensure safe and secure transfer of tokens.

```solidity
contract TokenizedResolver is SchemaResolver {
    using SafeERC20 for IERC20;

    IERC20 private immutable token;

    uint256 immutable fee = 10;

    constructor(address rs, address tokenAddr) SchemaResolver(rs) {
        token = IERC20(tokenAddr);
    }

    function onAttest(
        Attestation calldata attestation,
        uint256 value
    )
        internal
        virtual
        override
        returns (bool)
    {
        // Transfer attestation fee to from attester to resolver
        token.safeTransferFrom(attestation.attester, address(this), fee);
        return true;
    }

    function onRevoke(
        Attestation calldata attestation,
        uint256 value
    )
        internal
        virtual
        override
        returns (bool)
    {
        return true;
    }

    function onModuleRegistration(
        Module calldata module,
        uint256 value
    )
        internal
        override
        returns (bool)
    {
        return true;
    }

    function onPropagation(
        Attestation calldata attestation,
        address sender,
        address to,
        uint256 toChainId,
        address moduleOnL2
    )
        internal
        override
        returns (bool)
    {
        return true;
    }
}
```


### Example: ValueResolver

The ValueResolver contract is a straightforward schema resolver that verifies whether a precise amount of ETH has been included with an attestation. During the formation of the contract, a goal value is established. Throughout the attestation procedure, it assures that the inputted value aligns with this previously defined target value. If there's a match, the attestation is deemed valid.


```solidity
contract ValueResolver is SchemaResolver {
    uint256 immutable fee = 10;

    constructor(address rs) SchemaResolver(rs) { }

    function onAttest(
        Attestation calldata attestation,
        uint256 value
    )
        internal
        virtual
        override
        returns (bool)
    {
        return msg.value > fee;
    }

    function onRevoke(
        Attestation calldata attestation,
        uint256 value
    )
        internal
        virtual
        override
        returns (bool)
    {
        return true;
    }

    function isPayable() public pure override returns (bool) {
        return true;
    }

    function onModuleRegistration(
        Module calldata module,
        uint256 value
    )
        internal
        override
        returns (bool)
    {
        return true;
    }

    function onPropagation(
        Attestation calldata attestation,
        address sender,
        address to,
        uint256 toChainId,
        address moduleOnL2
    )
        internal
        override
        returns (bool)
    {
        return true;
    }
}
```
